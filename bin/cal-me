#!/usr/bin/perl -w
# Copyright Â© 2014 Shiv Menon <shiv@pobox.com>
#
# Permission to use, copy, modify, distribute, and sell this software and its
# documentation for any purpose is hereby granted without fee, provided that
# the above copyright notice appear in all copies and that both that
# copyright notice and this permission notice appear in supporting
# documentation.  No representations are made about the suitability of this
# software for any purpose.  It is provided "as is" without express or 
# implied warranty.
#
# Many sites (like Facebook) let you export events as .ics files which you can import into 
# iCal; however, sometimes those imported events are not editable. This script will munge 
# them into editability prior to importing them into iCal.

require 5;

use diagnostics;
use strict;
use File::Spec;
use open ":encoding(utf8)";

sub clean_ics($){
  my($file) = @_;
  open(my $in, "<", "$file") || die("$file: $!");
  open(my $out, ">", "$file.tmp") || die("$file.tmp: $!");
  while(<$in>) {
    next if ($_ =~ m/^(ORGANIZER|ATTENDEE|PARTSTAT)/);
    print $out $_;
  }
  close($in);
  close($out);

  rename "$file.tmp", "$file";
}

sub add_ics($){
  my ($file) = @_;
  my $filepath = File::Spec->rel2abs($file);
  my $script = <<"END_SCRIPT";
tell application "Finder" to open POSIX file "${filepath}"
END_SCRIPT
  system("/usr/bin/osascript", "-e", $script);
}

sub usage() {
  print STDERR "Usage: cal-me file.ics\n";
  exit 1;
}

sub main() {
  my ($file);
  while ($#ARGV >= 0) {
    $_ = shift @ARGV;
    if (!defined($file)) { $file = $_; }
    else { usage(); }
  }

  usage() unless (defined($file));
  clean_ics($file);
  add_ics($file);
}

main();
exit 0;
